//Title: Unapproved RMM Software Detected
//Description: Detects installation or use of unapproved RMM software. Based on lawndoc's UndocumentedRMM query.
//References: 
//  https://github.com/0x706972686f/RMM-Catalogue
//  https://github.com/lawndoc/AdvancedHuntingQueries/blob/f449fd78124543f2269dd6656017b3d1035f5df2/Detection-Rules/UndocumentedRMM.kusto
//Author: 
//Created: 2024/04/04
//Modified: 2024/04/16
//------------------------------------------------------------------
// EXCLUSIONS GO HERE, PLEASE LEAVE A COMMENT WITH EXPLANATION
let ApprovedRMM = datatable(Software:string, ApprovedDevice:string) 
[
    "Quick Assist", "",             // Installed on Windows by Default
    "TeamViewer", "",               // Approved RMM Software for IT
];
//Parses and formats the list from GitHub
let RMMCatalogue = materialize(
(externaldata(GitHubRMMList: string) [@"https://raw.githubusercontent.com/0x706972686f/RMM-Catalogue/main/rmm.csv"] with (format="txt", ignoreFirstRecord=True))
    | extend ParsedRMMList = parse_csv(GitHubRMMList)
    | extend 
        Software = tostring(ParsedRMMList[0]),
        Domain = tostring(ParsedRMMList[1]),
        Executables = parse_csv(tolower(tostring(ParsedRMMList[2]))),
        Category = tostring(ParsedRMMList[3])
    | mv-expand Executables = Executables
    | extend Executables = replace_string(tostring(Executables), "*.exe", "")
    | project Executables, Software)
;
//Could be changed to work with NRT if external data is allowed again. If so, comment all lines following this block out----------------
// DeviceProcessEvents
//     | where ActionType != "FileDeleted"
//     | extend Executables = tolower(FileName)
//     | where Executables in~ (RMMCatalogue)
// //Add RMM Exclusions here------------------------------------------------------------------------------
//     | where FolderPath !contains "TeamViewer"
//Finds all instances of RMM used according to the GitHub list
let RMMUsed = (
    RMMCatalogue
    | extend Executables = tolower(Executables)
    | join kind=inner (
        union DeviceFileEvents, DeviceProcessEvents, DeviceNetworkEvents
        | where ActionType != "FileDeleted"
        | extend Executables = tolower(FileName))
    on Executables)
;
//Creates a list of all whitelisted RMM software on endpoints
let ExcludedRMM = RMMUsed
    | join kind=inner ApprovedRMM on Software
    | where DeviceName contains ApprovedDevice
    | distinct Software, DeviceName
;
//Removes approved entries from full RMM list and displays final results
RMMUsed
    | join kind=anti ExcludedRMM on Software,DeviceName
    | summarize Timestamp=max(Timestamp) by Software, DeviceName
    | join kind=rightsemi RMMUsed on Timestamp,Software,DeviceName
    | project-reorder Timestamp, DeviceName, Software, Executables, FolderPath
    | sort by Timestamp desc
