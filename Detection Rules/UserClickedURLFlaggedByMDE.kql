//Title: User Clicked URL in Email Marked as Phishing/Malicious
//Status: TEST
//Description: This alert triggers if a user clicks a URL in an email that was labeled as phishing or malicious by MDE or if it was reported as phishing by user after being clicked.
//             It will query 30d worth of emails to catch any URLs that were clicked in emails sent a long time ago.
//References: 
//  N/A
//Author: CyberSpooon
//Created: 2024/08/06
//Modified: 2025/03/07
//Tags:
//  attack.initial_access
//  attack.T1566.002
//Falsepositives: If MDE misclassifies an email as phishing and a user clicks a link within the email.
//------------------------------------------------------------------
let timeframe = 30d;
let EmailAlerts=
// get only alerts containing email NetworkMessageIds
AlertEvidence
| where Timestamp >= ago(timeframe)
| where isnotempty(NetworkMessageId)
| join AlertInfo on AlertId
| where Title != @"User Clicked URL in Email Marked as Phishing/Malicious"
// get helpful fields and rename them
| extend ParsedAddFields=parse_json(AdditionalFields)
| extend Sender = ParsedAddFields.Sender
| extend EmailReceivedTimestamp = todatetime(ParsedAddFields.ReceivedDate)
| extend AlertGeneratedTimestamp=Timestamp1
| project AlertGeneratedTimestamp,AlertId,AlertTitle=Title,EmailReceivedTimestamp, NetworkMessageId,EmailSubject,AccountUpn,Sender
;
let UserClicks=
//get clicks associated with email alerts via NetworkMessageId
UrlClickEvents
| join EmailAlerts on (NetworkMessageId)
| project-away *1
| project-reorder Timestamp,Url,ActionType, IsClickedThrough, EmailReceivedTimestamp,NetworkMessageId, AccountUpn,IPAddress, EmailSubject,Sender,AlertGeneratedTimestamp,AlertTitle,AlertId
;
UserClicks

// The device correlation doesnt work with detection rules for some reason. need to figure out why
// Correlate user who clicked link to their registered device 
// DeviceLogonEvents
// | project DeviceId,DeviceName, AccountUpn=AccountName
// | where DeviceName !endswith "company.com" and AccountUpn endswith "company.com"
// | join UserClicks on (AccountUpn)
// | project-away *1
// | project-reorder Timestamp,Url,ActionType, IsClickedThrough, EmailReceivedTimestamp,NetworkMessageId, AccountUpn,IPAddress,DeviceId, DeviceName, EmailSubject,Sender,AlertGeneratedTimestamp,AlertTitle,AlertId
