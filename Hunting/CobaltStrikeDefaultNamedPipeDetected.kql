//Title: Suspicious Named Pipe Associated with Cobalt Strike Detected
//Status: TEST
//Description: This detection is looks for suspicious or default named pipes that are typically associated with Cobal Strike. 
//             Additionally, it will identify many named pipes in commonly used malleable Cobalt Strike profiles. 
//             It is a modified version of Falcon Force's Suspicious Named Pipes detection which can be found here: 
//             https://github.com/FalconForceTeam/FalconFriday/blob/master/Execution/T1559-WIN-001.md
//References: 
//      https://github.com/FalconForceTeam/FalconFriday/blob/master/Execution/T1559-WIN-001.md
//      https://medium.com/falconforce/falconfriday-suspicious-named-pipe-events-0xff1b-fe475d7ebd8
//      https://www.cobaltstrike.com/blog/learn-pipe-fitting-for-all-of-your-offense-projects
//      https://svch0st.medium.com/guide-to-named-pipes-and-hunting-for-cobalt-strike-pipes-dc46b2c5f575
//Author: FalconForceTeam, Alden James
//Created: 2024/08/27
//Modified: 2024/08/27
//Tags:
//      attack.execution
//      attack.T1559
//Falsepositives: 
//      No FPs identified
//------------------------------------------------------------------
let timeframe= 1d;
DeviceEvents
| where Timestamp >= ago(timeframe)
| where ActionType == "NamedPipeEvent"
| extend AdditionalFields=parse_json(AdditionalFields)
| extend ThreadId=tostring(AdditionalFields.ThreadId)
| extend PipeName=tostring(AdditionalFields.PipeName)
// creating string based variants of the processIDs for matching several times later
| extend InitiatingPID=tostring(InitiatingProcessId)
| extend InitiatingParentPID=tostring(InitiatingProcessParentId)
// Add exclusions or exceptions to this rule here
//| where NamedPipe != "Exception"
| where 
    PipeName contains @"msagent_" or
    PipeName contains @"MSSE-" or
    PipeName contains @"postex_" or
    PipeName contains @"status_" or
    PipeName contains @"mypipe-f" or
    PipeName contains @"mypipe-h" or
    PipeName contains @"ntsvcs_" or
    PipeName contains @"scerpc_" or
    PipeName contains @"mojo.5688.8052." or
// mojo is generated by Chrome(ium) browsers and teams and have distinct pattern including the (parent)ProcessId and ThreadId plus a random character string, CobaltStrike generates hex
        (PipeName matches regex @"\\mojo\.\d+\.\d+\." and not(PipeName matches regex @"\\mojo\.\d+\.\d+\.\d+$" or PipeName has InitiatingPID or PipeName has InitiatingParentPID or PipeName has ThreadId)) or
// chrome(ium) browsers sync processes have distinct pattern including the (parent)ProcessId and ThreadId plus a random character string, CobaltStrike generates hex
        (PipeName matches regex @"\\(edge|chrome)\.sync\.\d+\.\d+\." and not(PipeName matches regex @"\\(edge|chrome|edge\.sync|chrome\.sync)\.\d+\.\d+\.\d+$" or PipeName has InitiatingPID or PipeName has InitiatingParentPID or PipeName has ThreadId)) or
// PSHost is generated by PowerShell and has a distinct pattern including the (parent)ProcessId
        (PipeName matches regex @"\\PSHost\.\d+\." and not(PipeName matches regex @"\\PSHost\.\d+\.\d+\." or PipeName has InitiatingPID or PipeName has InitiatingParentPID)) or
// crashpad pipes have a distinct pattern including the ProcessId and a string of upper case characters
        (PipeName matches regex @"\\crashpad_" and not(PipeName matches regex @"\\crashpad_\d+_[A-Z]+" or PipeName has InitiatingPID or PipeName has InitiatingParentPID)) or
// firefox pipes have a distinct pattern including the ProcessId and 1-3 digits which are sequential for each new pipe
        (PipeName matches regex @"\\cubeb-pipe-" and not(PipeName matches regex @"\\cubeb-pipe-\d+_[0-9]{1-3}+" or PipeName has InitiatingPID)) or
// based on a list of public mallable profiles and a suffix that is a random HEX string
        (
                ( 
                PipeName contains @"win_svc" or
                PipeName contains @"ntsvcs" or
                PipeName contains @"scerpc" or
                PipeName contains @"status_" or
                PipeName contains @"SearchTextHarvester" or
                PipeName contains @"DserNamePipe" or
                PipeName contains @"wkssvc_" or
                PipeName contains @"scerpc_" or
                PipeName contains @"spoolss_" or
                PipeName contains @"CatalogChangeListener" or
                PipeName contains @"fullduplex_" or
                PipeName contains @"demoagent_" or
                PipeName contains @"PGMessagePipe" or
                PipeName contains @"MsFteWds" or
                PipeName contains @"postex_ssh_" or
                PipeName contains @"windows.update.manager" or
                PipeName contains @"\f4c3" or
                PipeName contains @"\f53f" or
                PipeName contains @"halfduplex_"
                ) 
                and PipeName matches regex @"[a-fA-F0-9]{2,10}$") or
        (PipeName matches regex @"\\pipe\\[0-9a-f]{7,10}" or PipeName matches regex @"\\pipe\\[0-9a-f]{8}")
