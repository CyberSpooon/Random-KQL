//Title: Cobalt Strike Default Named Pipe Detected
//Status: Experimental
//Description: Detects creation of default named pipes used by Cobalt Strike
//References: 
//  https://www.cobaltstrike.com/blog/learn-pipe-fitting-for-all-of-your-offense-projects
//  https://thedfirreport.com/2024/04/01/from-onenote-to-ransomnote-an-ice-cold-intrusion/
//Author: Alden James
//Date: 2024/04/03
//Modified: 2024/04/03
//Tags:
//  attack.command_and_control
//Falsepositives: Unknown
//------------------------------------------------------------------
let timeframe = 14d;
DeviceEvents
    | where Timestamp >= ago(timeframe)
    | where ActionType == "NamedPipeEvent"
    | extend ParsedAddFields = parse_json(AdditionalFields)
    | extend NamedPipeEnd = tostring(ParsedAddFields.NamedPipeEnd)
    | extend PipeName = tostring(ParsedAddFields.PipeName)
    | where 
        ((PipeName contains @"MSSE" and PipeName contains @"server") 
        or PipeName contains @"status" 
        or PipeName contains @"msagent" 
        or PipeName contains @"postex" 
        or PipeName contains @"postex_ssh")
    | project-reorder Timestamp,DeviceName,ActionType,PipeName,InitiatingProcessFileName,InitiatingProcessFolderPath
