//Title: Potential Credentials Stored on Local User Machine
//Status: EXPERIMENTAL
//Description: This detection identifies instances where users are saving 
//             credentials in plaintext files on their local machine.
//References: 
//      
//Author: CyberSpooon
//Created: 2024/10/24
//Modified: 2024/10/24
//Tags:
//      attack.Credential_Access
//      attack.T1552
//Falsepositives: 
//      Password Policy files, Password reset instructions
//------------------------------------------------------------------
let FileExtensionList = datatable (FileExtensionList:string)
[
    ".doc",
    ".docx",
    ".xls",
    ".xlsx",
    ".txt",
    ".csv",
];
DeviceFileEvents
| where ActionType != "FileDeleted"
| where Timestamp > ago(30d)
// list words to search for
| where
    FileName contains "password"
    or FileName contains "passwords"
    or FileName contains "pwd"
    or FileName contains "login"
    or FileName contains "logon"
// Start FileName Exclusions ------------------------------------------------
| where 
    FileName !contains "example.txt"
// End FileName Exclusions -------------------------------------------------
| extend FileExtension = extract(@'\.([^\.]+)$', 0, FileName)
| where FileExtension in~ (FileExtensionList)
| summarize Timestamp=max(Timestamp) by DeviceName, AccountUpn=tolower(InitiatingProcessAccountUpn), FileName, FolderPath,ActionType
| project Timestamp,DeviceName,AccountUpn,ActionType, FileName,FolderPath
| sort by Timestamp desc 
